apply plugin: 'com.gradleup.shadow'

import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
	delete "$rootDir/out/"
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	from(sourceSets.main.resources.srcDirs) {
		include '**/*.yml'
		filter(ReplaceTokens, tokens: [VERSION: project.version])
	}
}

dependencies {
	implementation project(':duels-api')
	implementation project(':duels-worldguard')

	compileOnly 'org.projectlombok:lombok:1.18.38'
	annotationProcessor 'org.projectlombok:lombok:1.18.38'

	implementation 'org.apache.commons:commons-lang3:3.18.0'
	implementation 'net.kyori:adventure-text-serializer-ansi:4.17.0'
	compileOnly 'com.mojang:authlib:3.13.56'
	compileOnly 'me.clip:placeholderapi:2.11.6'
	compileOnly 'com.github.timderspieler:DeluxeCombat-API:1.5.1'
	compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
	compileOnly('net.essentialsx:EssentialsX:2.21.2')
	implementation 'space.arim.morepaperlib:morepaperlib:0.4.4'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
	implementation 'org.mongodb:mongodb-driver-sync:5.1.2'
	implementation 'redis.clients:jedis:5.1.5'
	implementation 'org.apache.commons:commons-pool2:2.12.0'

	compileOnly 'com.infernalsuite.asp:api:4.0.0-SNAPSHOT'
	implementation 'com.infernalsuite.asp:file-loader:4.0.0-SNAPSHOT'
	implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/'}
    maven { url 'https://libraries.minecraft.net' }
    maven { url 'https://jitpack.io' }
}

shadowJar {
	getDestinationDirectory().set(file("$rootDir/out/"))

	final String archiveName = parent.name + '-' + project.version + '.jar'
	getArchiveFileName().set(archiveName)

	dependencies {
		include(project(':duels-api'))
		include(project(':duels-worldguard'))
		include(dependency('com.fasterxml.jackson.core:.*'))
		include(dependency('space.arim.morepaperlib:.*'))
		include(dependency('org.mongodb:.*'))
		include(dependency('redis.clients:.*'))
		include(dependency('org.apache.commons:commons-pool2:.*'))
		include(dependency('org.apache.commons:commons-lang3:.*'))
		include(dependency('net.kyori:adventure-text-serializer-ansi:.*'))
		include(dependency('com.infernalsuite.asp:file-loader:.*'))
		include(dependency('co.aikar:acf-paper:.*'))
	}

	final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
	relocate 'com.fasterxml.jackson', 'com.emmerichbrowne.duels.shaded.jackson'
	relocate 'space.arim.morepaperlib', group + 'morepaperlib'
	relocate 'com.mongodb', group + 'mongodb'
	relocate 'org.bson', group + 'bson'
	relocate 'redis.clients', group + 'redis'
	relocate 'org.apache.commons.pool2', group + 'commons.pool2'
	relocate 'org.apache.commons.lang3', group + 'commons.lang3'
	relocate 'com.infernalsuite.asp.loader.file', group + 'asp.loader.file'
	relocate 'co.aikar.commands', group + 'acf'
	relocate 'co.aikar.locales', group + 'acflocales'
}

// To build Duels plugin jar, run './gradlew clean build'.
build {
	dependsOn(shadowJar)
}
